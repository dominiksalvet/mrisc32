% -*- mode: latex; tab-width: 2; indent-tabs-mode: nil; -*-
%------------------------------------------------------------------------------
% MRISC32 ISA Manual - Examples.
%
% This work is licensed under the Creative Commons Attribution-ShareAlike 4.0
% International License. To view a copy of this license, visit
% http://creativecommons.org/licenses/by-sa/4.0/ or send a letter to
% Creative Commons, PO Box 1866, Mountain View, CA 94042, USA.
%------------------------------------------------------------------------------

% We do assembler examples in single-column mode to better fit the code.
\onecolumn

\chapter{Examples}

This is a non-normative section that contains programs that exemplify various
aspects of the MRISC32 instruction set architecture.

\section{Vector operation}

\subsection{saxpy}

\begin{lstlisting}[style=assembler]
; void saxpy(size_t n, const float a, const float *x, float *y)
; {
;   for (size_t i = 0; i < n; i++)
;     y[i] = a * x[i] + y[i];
; }
;
; Register arguments:
;   s1 - n
;   s2 - a
;   s3 - x
;   s4 - y

saxpy:
    bz    s1, 2f        ; Nothing to do?
    cpuid s5, z, z      ; Query the maximum vector length
1:
    minu  vl, s5, s1    ; Define the operation vector length
    sub   s1, s1, vl    ; Decrement loop counter
    ldw   v1, s3, #4    ; Load x (element stride = 4 bytes)
    ldw   v2, s4, #4    ; Load y
    fmul  v1, v1, s2    ; x * a
    fadd  v1, v1, v2    ; + y
    stw   v1, s4, #4    ; Store y
    ldea  s3, s3, vl*4  ; Increment address (x)
    ldea  s4, s4, vl*4  ; Increment address (y)
    bnz   s1, 1b
2:
    ret
\end{lstlisting}

\subsection{Linear interpolation}

Linear interpolation can be implemented using vector gather load. Here is an
example of one-dimensional floating-point interpolation.

\begin{lstlisting}[style=assembler]
; void lerp(size_t n, const float t0, const float dt, const float *x, float *y)
; {
;   float t = t0;
;   for (size_t i = 0; i < n; i++)
;   {
;     int k = (int)t;
;     float w = t - (float)k;
;     y[i] = x[k] + w * (x[k+1] - x[k]);
;     t += dt;
;   }
; }
;
; Register arguments:
;   s1 - n
;   s2 - t0
;   s3 - dt
;   s4 - x
;   s5 - y

lerp:
    bz    s1, 2f        ; Nothing to do?

    cpuid s6, z, z      ; Query maximum vector length
    mov   vl, s6

    add   s8, s4, #4    ; s8 = &x[1]
    itof  s7, s6, z

    ldea  v1, z, #1
    itof  v1, v1, z
    fmul  v1, v1, s3    ; v1 = dt * [0.0, 1.0, 2.0, ...]

    fmul  s7, s3, s7    ; s7 = dt * maximum vector length
1:
    minu  vl, s6, s1    ; Define the operation vector length
    sub   s1, s1, vl    ; Decrement loop counter

    ftoi  v2, v1, z     ; v2 = integer indexes (k)
    itof  v3, v2, z
    fsub  v3, v1, v3    ; v3 = interpolation weight (w)

    ldw   v4, s4, v2*4  ; Load x[k]
    ldw   v5, s8, v2*4  ; Load x[k+1]

    fsub  v5, v5, v4
    fmul  v5, v5, v3
    fadd  v5, v4, v5    ; v5 = x[k] + w * (x[k+1] - x[k])

    stw   v5, s5, #4    ; Store y (element stride = 4 bytes)

    ldea  s5, s5, vl*4  ; Increment address (y)
    fadd  v1, v1, s7    ; Increment t
    bnz   s1, 1b
2:
    ret
\end{lstlisting}

\subsection{Reverse bytes}

Reversing a byte array (e.g. for horizontal mirroring of an image) can be
achieved by copying 32-bit words in reverse order (using a negative stride when
storing the words), in combination with reversing the bytes of each individual
word using the SHUF instruction.

\begin{lstlisting}[style=assembler]
; void revbytes(size_t n, const float *x, float *y)
; {
;   for (size_t i = 0; i < n; i++)
;     y[n-1-i] = x[i];
; }
;
; Register arguments:
;   s1 - n
;   s2 - x
;   s3 - y

revbytes:
    bz    s1, 2f        ; Nothing to do?
    add   s4, s1, #-4
    add   s3, s3, s4    ; s3 = &y[n-4]
    cpuid s4, z, z      ; Query the maximum vector length
    lsl   s5, s4, #2    ; s5 = 4 * max vector length
1:
    minu  vl, s4, s1    ; Define the operation vector length
    sub   s1, s1, vl    ; Decrement loop counter
    ldw   v1, s2, #4    ; Load x (element stride = 4 bytes)
    shuf  v1, v1, #0b000001010011  ; Reverse bytes of each word
    stw   v1, s3, #-4   ; Store y (element stride = -4 bytes)
    add   s2, s2, s5    ; Increment address (x)
    sub   s3, s3, s5    ; Decrement address (y)
    bnz   s1, 1b
2:
    ret
\end{lstlisting}

\twocolumn
